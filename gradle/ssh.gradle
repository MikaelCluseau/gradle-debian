
project.Scp = Scp.class

configurations {
    sshAntTask
}

dependencies {
    sshAntTask "org.apache.ant:ant-jsch:1.8.2"
}

tasks.withType(Scp) {
    sshAntClasspath = configurations.sshAntTask
}

class Scp extends DefaultTask {
    @InputFiles
    FileCollection sshAntClasspath

    def from(Object path) {
        source << path
    }

    @InputDirectory
    File sourceDir

    @Input
    String host

    @Input
    String userName

    @Input
    String password

    @Input
    String destinationDir

    @TaskAction
    def executeActions() {
        ant.taskdef(name: 'scp',
                classname: 'org.apache.tools.ant.taskdefs.optional.ssh.Scp',
                classpath: sshAntClasspath.asPath,
                loaderref: 'ssh')
        ant.taskdef(name: 'sshexec',
                classname: 'org.apache.tools.ant.taskdefs.optional.ssh.SSHExec',
                classpath: sshAntClasspath.asPath,
                loaderref: 'ssh')
        ant.sshexec(host: host, username: userName, password: password, command: "mkdir -p ${destinationDir}")
        ant.scp(remotetodir: "${userName}@${host}:${destinationDir}", password: password) {
            fileset(dir: sourceDir)
        }
    }
}
